(let-fn print-log ()
  (let y (- :height 6))
  (let y2 (- :height 1))
  (for s in (rev (log:get))
    (when (< y y2)
      (print 2 y s))
    (inc! y)))


(let-fn draw-tooltips ()
  (let white (Color 1 1 1)
    grey (Color 0.3 0.3 0.3)
    yellow (Color 1 1 0))
  (let map (ecs:fetch 'map))
  (let tooltip (arr))
  (for (entity (name pos)) in (ecs:query Name Position)
    (let idx (.xy-idx map [pos 'x] [pos 'y]))
    (when (and
            (eq? [pos 'x] [:mouse 0])
            (eq? [pos 'y] [:mouse 1])
            (.visible? map idx))
      (push! tooltip [name 'name])))

  (when (not (empty? tooltip))
    (let width 0)
    (for s in tooltip
      (when (< width (len s))
        (= width (len s))))
    (inc! width 2)

    (if (> [:mouse 0] (- :width 10))
      (do
        (let arrow-pos (Point (- [:mouse 0] 2) [:mouse 1]))
        (let left-x (- [:mouse 0] width))
        (let y [:mouse 1])
        (for s in tooltip
          (print left-x y s white grey)
          (let padding (- (- width (len s)) 1))
          (forn (i 0 padding)
            (print (- [arrow-pos 'x] i) y "" white grey))
          (inc! y))
        (print [arrow-pos 'x] [arrow-pos 'y] "╟─" white grey))
      (do ;else
        (let arrow-pos (Point (+ [:mouse 0] 1) [:mouse 1]))
        (let left-x (+ [:mouse 0] 2))
        (let y [:mouse 1])
        (for s in tooltip
          (print (+ left-x 1) y s white grey)
          (let padding (- width (len s) 1))
          (forn (i 0 padding)
            (print (+ [arrow-pos 'x] 1 i) y "" white grey))
          (inc! y))
        (print [arrow-pos 'x] [arrow-pos 'y] "─╢" white grey)))))

(defn draw-ui ()
  ; log rectangle
  (let player (ecs:fetch 'player))
  (let stats (ecs:get-cmp player CombatStats))
  (draw-box 0 (- :height 7) :width 7)
  (print 12 (- :height 7) "HP: {[stats 'hp]}/{[stats 'max-hp]}" (Color 1 1 0) :bg-color)

  ; log messages
  (print-log)

  ; health bar
  (draw-h-bar 28 (- :height 7) 51 [stats 'hp] [stats 'max-hp] (Color 1 0 0) :bg-color)

  ; mouse cursor
  (set [:mouse 0] [:mouse 1] (ss-idx 8 248) (Color 1 0 1) #n 3)

  (draw-tooltips))
